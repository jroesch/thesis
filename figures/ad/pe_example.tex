\begin{figure}
  \centering
  % \texttt{\bf Identity Function}
  \judgbox{\texttt{\bf Identity Function}}{}
    \begin{minted}{rust}
fn <s, bt>(%d: Tensor[s, bt]) {
  %d
}
    \end{minted}

  \judgbox{\texttt{\bf Post-AD}}{}

    \begin{minted}{rust}
fn <s, bt>(%d: Tensor[s, bt]) {
  let %x = ref(fn () { () });
  let %x1 = (%d, ref(zeros_like(%d)));
  let %x2 =
    (fn <s, bt>(
      %d1: (Tensor[s, bt],
            ref(Tensor[s, bt]))) {
      %d1
    })(%x1);
  %x2.1 := ones_like(%x2.0);
  let %x3 = read(%x)();
  (%x2.0, (read(%x1.1),))
}
    \end{minted}
  \judgbox{\texttt{\bf Post-PE}}{}

    \begin{minted}{rust}
fn <s, bt>(%d: Tensor[s, bt]) {
  let %x = fn () {
    let %x1 = ();
    %x1
  };
  let %x2 = ref(%x);
  let %x3 = zeros_like(%d);
  let %x4 = ref(%x3);
  let %x5 = (%d, %x4);
  let %x6 =
    fn <s, bt>(
      %d1: (Tensor[s, bt],
            ref(Tensor[s, bt]))) {
      %d1
    };
  let %x7 = ones_like(%d);
  %x4 := %x7;
  let %x8 = ();
  let %x9 = (%x7,);
  let %x10 = (%d, %x9);
  %x10
}
    \end{minted}

  \judgbox{\texttt{\bf Post-DCE}}{}

    \begin{minted}{rust}
fn <s, bt>(%d: Tensor[s, bt]) {
  (%d, (ones_like(%d),))
}
  \end{minted}
  \caption{
    Example of running the compiler pass pipeline for AD on the identity
    function.
    First, we run the base AD pass on the original function (described in Section \ref{sec:autodiff}).
    Then, we run the partial evaluator,
      which primarily optimizes away the reads and calls in \texttt{\%x2} and
      \texttt{\%x3} in post-AD.
    Since it conservatively determines whether a subexpression is effectful,
      it generates many bindings which are dead code.
    At this point, we run the dead code elimination pass to crunch the code back down.
  }
  \label{fig:pe-example}
\end{figure}
